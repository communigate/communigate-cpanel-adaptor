// ============================================= //
//                   List files                  //
//                                               //
// Version 1.0                                   //
// Anton Katsarov <anton@webface.bg>             //
// ============================================= //
//
// Description:
//  Lists Accounts files and folders
//

function sizeReadable(size, type) forward;
function dateReadable(theDate) forward;
function getSubscriptions() forward;

function list() is
    // Check if private Folder exists
    rootFolder = ListStorageFiles("");
    baseFound = false;
    for i = 0  while i < Length(rootFolder) by i+= 1 loop
      if !IsNumber(rootFolder.(rootFolder[i]).STFileSize) && rootFolder[i] == "private" then
        baseFound = true;
      end if;
      exitif baseFound;
    end loop;
    if !baseFound then
      creationResult = CreateStorageDirectory("private");
    end if;

    //List Folders
    theSet = NewDictionary();
    theSet.Folders = NewArray();
    theSet.Files = NewArray();
    folder = "private";
    path = GetHTTPParameter("path",0);
    if (FindSubstring(path, "~") == 0) then
      theSet.Shared = true;
      folder = path;
      theSet.path = path;
      path = String(FindRegEx(path,"^.+/"));
      path = Substring(path, 2, Length(path) - 5);
      theSet.Upper = path;
      // if (FindSubstring(theSet.path, "/private") < 0) then
      //   folder = "private";
      //   theSet.Upper = null;
      // end if;
    elif IsString(path) && path != "" && path != " " then
      folder = folder + "/" + path;
      theSet.path = path;
      path = String(FindRegEx(path,"^.+/"));
      path = Substring(path, 2, Length(path) - 5);
      if !IsString(path) then
        path = " ";
      end if;
      theSet.Upper = path;
    end if;
    listedFolder = ListStorageFiles(folder);
    if listedFolder == null then
      theSet.notFound = folder;
    else
      for i = 0 while i < Length(listedFolder) by i+=1 loop
        theElement = NewDictionary();
        exitif !IsString(listedFolder[i]);
        theElement.Modified = dateReadable(listedFolder.(listedFolder[i]).STModified);
        theElement.Name = listedFolder[i];
        theElement.Path = theSet.path || " ";
        ext = String(FindRegEx(ToLowerCase(listedFolder[i]),"[.][a-z0-9]+$"));
        theElement.Ext = Substring(ext, 2, Length(ext) - 3);
        if IsNumber(listedFolder.(listedFolder[i]).STFileSize) then
          theElement.Size = sizeReadable(listedFolder.(listedFolder[i]).STFileSize, "B");
          theSet.Files.addElement(theElement);
        else
          if listedFolder[i] != "IM" && listedFolder[i] != "_upload" && listedFolder[i] != "settings" && listedFolder[i] != "logs" || folder !="private" then
            theSet.Folders.addElement(theElement);
          end if;
        end if;
      end loop;
    end if;
    if folder == "private" then
      theSet.Subscriptions = getSubscriptions();
    end if;
    return theSet;
end function;

function sizeReadable(size, type) is
  if IsNumber(size) then
    if size > 1024 then
      localType = "B";
      if type == "B" then
        localType = "kB";
      elif type == "kB" then
        localType = "MB";
      elif type == "MB" then
        localType = "GB";
      elif type == "GB" then
        localType = "TB";
      end if;
      size = Number(sizeReadable(size/1024, localType));
      return String(size) + localType;
    else
    return String(size) + type;
    end if;
  else
    return "O" + type;
  end if;
end function;

function dateReadable(theDate) is
  stringDate = String(theDate);
  return Substring(stringDate,0,10) + " " + Substring(stringDate,11,20);
end function;

function getSubscriptions() is
    s = null;
    cmd = "GETFILESUBSCRIPTION *";
    rc = ExecuteCLI(cmd);
    if not IsString(rc) then
        s = Vars().executeCLIResult;
    end if;
    return s;
end function;
